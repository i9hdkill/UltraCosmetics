allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = 'be.isach'
    version = '2.5-PRERELEASE'

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

boolean local = false

if (local) libsDirName = '../testserver/plugins'

subprojects {

    repositories {
        mavenLocal()
        mavenCentral()
        // Vault
        maven {
            url 'http://nexus.hc.to/content/repositories/pub_releases'
        }
        // Lib's Disguises
        maven {
            url 'http://repo.md-5.net/content/groups/public/'
        }

        maven {
            url 'https://repo.isach.be'
        }

        // BungeeCord
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }

        // PlaceholderAPI
        maven {
            url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
        }

        // PlayerPoints
        maven {
            url 'https://repo.dakanilabs.com/repository/public/'
        }
    }
}

processResources {
    inputs.property("version", project.version)

    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'VERSION': project.version.split('-')[0]
    ]
}

subprojects.each { subproject -> evaluationDependsOn( subproject.path ) }

task allVersions(type: Jar) {
    baseName = rootProject.name + '-All'
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
        from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

}

artifacts {
    archives allVersions
}